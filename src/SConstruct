import os, glob, string

nodes = ['.']
dirs = []
vs = []

while nodes:
  node = nodes.pop()
  b = os.path.basename(node)
  if node.endswith('.v'):
    vs += [node]
  if os.path.isdir(node) and node != "./broken":
    dirs += [node]
    nodes += glob.glob(node + '/*')

Rs = ' -R interfaces interfaces -R theory theory -R orders orders -R varieties varieties -R categories categories '
  # Todo: This sucks. We'd like to use "-R . ''", but this doesn't work. See Coq bug 2263.
Rs_and_Is = Rs + ' -I implementations -I misc '

coqc = 'coqc ' + Rs_and_Is + ' $SOURCE'

env = DefaultEnvironment(ENV = os.environ)

def add_glob(target, source, env):
  base, _ = os.path.splitext(str(target[0]))
  target.append(base + ".glob")
  return target, source

env.Append(BUILDERS =
  {'Coq' : Builder(
    action = coqc,
    suffix = '.vo',
    src_suffix = '.v',
    emitter = add_glob)})

vos = globs = []
for node in vs:
  vo, glob = env.Coq(node)
  vos += [vo]
  globs += [glob]

env.Command(env.Dir('coqdoc'), vs + vos + globs + [File('tools/coqdoc.css.diff')],
  [ Delete('coqdoc')
  , Mkdir('coqdoc')
  , 'coqdoc -utf8 --toc -g --no-lib-name -d $TARGET' + Rs + ' '.join(vs)
  , 'patch --backup $TARGET/coqdoc.css tools/coqdoc.css.diff'])

os.system('coqdep ' + Rs_and_Is + ' '.join(vs) + ' > deps')
ParseDepends('deps')

Alias('coqide', Command(['dummy'], [], 'coqide ' + Rs_and_Is + ' &'))

Default('implementations', 'theory', 'categories', 'orders', 'varieties')

open('coqidescript', 'w').write('#!/bin/sh\ncoqide' + Rs_and_Is + '$@ \n')
